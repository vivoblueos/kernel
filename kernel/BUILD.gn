# Copyright (c) 2025 vivo Mobile Communication Co., Ltd.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#       http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/boards/${board}.gni")
import("//build/templates/build_template.gni")
import("//build/toolchain/blueos.gni")

_kernel_default_cfgs = [
  "scheduler=\"global\"",
  "${build_type}",
]

group("check_kernel") {
  testonly = true
  deps = [
    ":run_integration_test",
    ":run_unittest",
  ]
}

group("check_kernel_by_clippy") {
  testonly = true
  deps = [
    ":blueos_clippy",
    ":kernel_integration_test_clippy",
    ":kernel_unittest_clippy",
    "//external/vendor/semihosting-0.1.20:semihosting",
  ]
}

group("check_kernel_probe") {
  testonly = true
  deps = [ ":run_unittest" ]
}

_shared_deps = [
  ":atomic",
  "//external/vendor/bitflags-2.10.0:bitflags",
  "//external/vendor/cfg-if-1.0.4:cfg_if",
  "//external/vendor/const-default-1.0.0:const_default",
  "//external/vendor/embedded-io-0.6.1:embedded_io",
  "//external/vendor/heapless-0.8.0:heapless",
  "//external/vendor/log-0.4.28:log",
  "//external/rust-fatfs/v0.4:fatfs",
  "//external/vendor/safe-mmio-0.2.5:safe_mmio",
  "//external/vendor/spin-0.9.8:spin",
  "//external/vendor/thiserror-2.0.17:thiserror",
  "//external/vendor/tock-registers-0.9.0:tock_registers",
  "//external/vendor/zerocopy-0.8.27:zerocopy",
  "//kernel/driver:blueos_driver",
  "//kernel/hal:blueos_hal",
  "//kernel/header:blueos_header",
  "//kernel/infra:blueos_infra",
  "//kernel/kconfig:blueos_kconfig",
  "//libc",
]
_shared_deps += board_deps

if (coverage || profile) {
  _shared_deps += [
    "//external/vendor/minicov-0.3.7:minicov",
    "//external/vendor/semihosting-0.1.20:semihosting",
  ]
}

build_rust("kernel_unittest") {
  testonly = true
  crate_name = "kernel_unittest"
  crate_type = "bin"
  sources = [ "src/lib.rs" ]
  edition = "2021"
  proc_macro_deps = [
    "//external/vendor/paste-1.0.15:paste",
    "//external/vendor/delegate-0.13.4:delegate",
    "//kernel/macro:blueos_macro",
    "//kernel/test_harness:blueos_test_macro",
  ]
  rustenv = [ "TARGET_BOARD=${board}" ]
  cfgs = _kernel_default_cfgs
  deps = _shared_deps
  deps += [ "//external/vendor/semihosting-0.1.20:semihosting" ]
  configs += [
    "//kernel/kconfig:kconfigs",
    "//build/boards/${board}:kernel_config",
  ]
  rustflags = [
    "--test",
    "-Zpanic-abort-tests",
  ]
}

run_clippy("kernel_unittest_clippy") {
  testonly = true
  deps = [ ":kernel_unittest" ]
}

build_rust("kernel_integration_test") {
  testonly = true
  crate_type = "bin"
  sources = [ "tests/integration_test.rs" ]
  edition = "2021"
  proc_macro_deps = [ "//kernel/test_harness:blueos_test_macro" ]
  deps = [
           ":blueos",
           "//external/vendor/byteorder-1.5.0:byteorder",
           "//external/vendor/semihosting-0.1.20:semihosting",
           "//kernel/rsrt",
           "//kernel/scal:blueos_scal",
           "//libc",
         ] + board_deps
  if (coverage || profile) {
    deps += [ "//external/vendor/minicov-0.3.7:minicov" ]
  }
  cfgs = _kernel_default_cfgs
  configs += [
    "//kernel/kconfig:kconfigs",
    "//build/boards/${board}:kernel_config",
  ]
  rustflags = [
    "--test",
    "-Zpanic-abort-tests",
  ]
}

run_clippy("kernel_integration_test_clippy") {
  testonly = true
  deps = [ ":kernel_integration_test" ]
}

build_rust("blueos") {
  crate_type = "rlib"
  sources = [ "src/lib.rs" ]
  edition = "2021"
  proc_macro_deps = [
    "//external/vendor/paste-1.0.15:paste",
    "//external/vendor/delegate-0.13.4:delegate",
    "//kernel/macro:blueos_macro",
  ]
  rustenv = [ "TARGET_BOARD=${board}" ]
  cfgs = _kernel_default_cfgs
  deps = _shared_deps
  configs += [
    "//kernel/kconfig:kconfigs",
    "//build/boards/${board}:kernel_config",
  ]
}

run_clippy("blueos_clippy") {
  deps = [ ":blueos" ]
}

cbindgen("rust_wrapper") {
  sources = [ "cbindgen.toml" ]
  args = [
           "--quiet",
           "-c",
         ] + rebase_path([ "cbindgen.toml" ], root_build_dir) +
         rebase_path([ "." ], root_build_dir)
}

gen_qemu_runner("integration_test_runner") {
  testonly = true
  semihosting = true
  img = ":kernel_integration_test"
  qemu = qemu_exe
  machine = machine
  qemu_args = qemu_extra_args
  net_args = qemu_net_args
  block_img = "integration_test_block.img"
  block_args = qemu_block_args
}

run_qemu_check("run_integration_test") {
  testonly = true
  runner = ":integration_test_runner"
  if (coverage) {
    img = ":kernel_integration_test"
    checker = "src/coverage.checker"
  } else {
    checker = "tests/integration.checker"
  }
}

if (defined(use_defmt) && use_defmt) {
  gen_probe_runner("unittest_runner") {
    testonly = true
    img = ":kernel_unittest"
    chip = "$chip"
  }
} else {
  gen_qemu_runner("unittest_runner") {
    testonly = true
    img = ":kernel_unittest"
    qemu = "$qemu_exe"
    machine = "$machine"
    qemu_args = qemu_extra_args
    block_img = "unittest_block.img"
    block_args = qemu_block_args
    semihosting = true
  }
}

run_qemu_check("run_unittest") {
  testonly = true
  runner = ":unittest_runner"
  if (coverage) {
    img = ":kernel_unittest"
    checker = "src/coverage.checker"
  } else {
    checker = "src/unittests.checker"
  }
}

static_library("atomic") {
  sources = [ "src/sync/libatomic/atomic.c" ]
}
