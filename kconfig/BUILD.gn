# Copyright (c) 2025 vivo Mobile Communication Co., Ltd.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#       http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/templates/build_template.gni")
import("//kernel/features.gni")

action("generate_c_header") {
  script = "//kernel/kconfig/src/generate_c_header.py"
  inputs = [
    "config/Kconfig",
    "config/${board}/${build_type}/defconfig",
    "//kernel/kernel/src/boards/${board}/Kconfig",
    "//kernel/kernel/src/Kconfig",
    "//kernel/kernel/src/allocator/Kconfig",
    "//kernel/kernel/src/scheduler/Kconfig",
    "//kernel/kernel/src/vfs/Kconfig",
    "//kernel/kernel/src/net/Kconfig",
  ]
  output_headers = rebase_path("${target_gen_dir}/autoconf.h")
  outputs = [ "${target_gen_dir}/autoconf.h" ]
  args = [
    "--kconfig",
    rebase_path("config/Kconfig"),
    "--board",
    "${board}",
    "--build_type",
    "${build_type}",
    "--output_headers",
    "${output_headers}",
  ]
}

bindgen("autoconf_header") {
  bindgen_flags = [
    "--use-core",
    "--ctypes-prefix",
    "core::ffi",
    "--no-layout-tests",
  ]
  sources = get_target_outputs(":generate_c_header")
  configs += [ "//build/boards/${board}:kernel_config" ]
  deps = [ ":generate_c_header" ]
}

action("parse_const") {
  script = "//kernel/kconfig/src/parse_const.py"
  inputs = get_target_outputs(":autoconf_header")
  output = rebase_path("${target_gen_dir}/kconfig.rs")
  input = rebase_path(inputs[0])
  outputs = [ "${target_gen_dir}/kconfig.rs" ]
  args = [
    "--src",
    "${input}",
    "--out",
    "${output}",
  ]
  deps = [ ":autoconf_header" ]
}

build_rust("blueos_kconfig") {
  crate_type = "rlib"
  sources = get_target_outputs(":parse_const")
  edition = "2021"
  deps = [ ":parse_const" ]
}

_kernel_configs = exec_script("//kernel/kconfig/src/parse_rustflags.py",
                              [
                                "--kconfig",
                                rebase_path("//kernel/kconfig/config/Kconfig"),
                                "--board",
                                "${board}",
                                "--build_type",
                                "${build_type}",
                              ],
                              "list lines")

config("kconfigs") {
  rustflags = []
  foreach(cfg, _kernel_configs) {
    rustflags += [
      "--cfg",
      cfg,
    ]
  }
}
