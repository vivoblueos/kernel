# Copyright (c) 2025 vivo Mobile Communication Co., Ltd.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#       http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/templates/build_template.gni")
import("//build/toolchain/blueos.gni")

shared_deps = [
  "//kernel/infra:blueos_infra",
  "//kernel/kernel:blueos",
]

build_rust("blueos_adapter") {
  sources = [ "src/lib.rs" ]
  edition = "2021"
  deps = shared_deps
}

run_clippy("blueos_adapter_clippy") {
  deps = [ ":blueos_adapter" ]
}

build_rust("blueos_adapter_unittest") {
  testonly = true
  crate_name = "blueos_adapter_unittest"
  crate_type = "bin"
  sources = [ "src/lib.rs" ]
  edition = "2021"
  proc_macro_deps = [ "//kernel/test_harness:blueos_test_macro" ]
  deps = [ "//external/vendor/semihosting-0.1.20:semihosting" ] + shared_deps
  configs += [
    "//kernel/kconfig:kconfigs",
    "//build/boards/${board}:kernel_config",
  ]
  rustflags = [ "--test" ]
}

run_clippy("blueos_adapter_unittest_clippy") {
  testonly = true
  deps = [ ":blueos_adapter_unittest" ]
}

gen_qemu_runner("blueos_adapter_unittest_runner") {
  testonly = true
  img = ":blueos_adapter_unittest"
  qemu = "$qemu_exe"
  machine = "$machine"
  qemu_args = qemu_extra_args
  block_img = "adapter_unittest_block.img"
  block_args = qemu_block_args
  semihosting = true
}

run_qemu_check("run_unittest") {
  testonly = true
  runner = ":blueos_adapter_unittest_runner"
  if (coverage) {
    img = ":blueos_adapter_unittest"
    checker = "//kernel/adapter/coverage.checker"
  } else {
    checker = "//kernel/adapter/unittests.checker"
  }
}
